/*
 * Copyright (C) 2016 Source Allies, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: "com.github.kt3k.coveralls"
apply from: '../test_counter.gradle'
apply from: 'jacoco.gradle'

def propsFile = rootProject.file('keystore.properties')
def props = new Properties()
props.load(new FileInputStream(propsFile))

propsFile = rootProject.file('api_keys.properties')
def apiKeys = new Properties()
apiKeys.load(new FileInputStream(propsFile))

def buildNumber = System.getenv("BUILD_NUMBER") as Integer ?: Integer.parseInt(VERSION_CODE)

android {
    compileSdkVersion Integer.parseInt(COMPILE_SDK_VERSION)
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.sourceallies.android.zonebeacon"
        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(TARGET_SDK_VERSION)
        versionName VERSION_NAME + "." + buildNumber
        versionCode buildNumber
        project.archivesBaseName = "Zone-Beacon-${versionName}"

        resValue "string", "nearby_api_key", "\"${apiKeys['NEARBY']}\""
    }

    signingConfigs {
        debug {
            storeFile file("../debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("../keystore.jks")
            storePassword props['keystorepassword']
            keyAlias props['keyalias']
            keyPassword props['keypassword']
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            versionNameSuffix "-DEBUG"
            signingConfig signingConfigs.debug
        }
        release {
            // proguard was not working with roboguice
            //minifyEnabled true
            //shrinkResources true
            signingConfig signingConfigs.release
            //zipAlignEnabled true
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        disable 'GoogleAppIndexingWarning', 'AllowBackup', 'InvalidPackage', 'IconMissingDensityFolder',
                'UnusedAttribute', 'ClickableViewAccessibility', 'IconDensities', 'GradleDependency',
                'UseSparseArrays'
        textReport true
        textOutput 'stdout'
        xmlReport false
        htmlReport true
    }
}

dependencies {

    provided "org.projectlombok:lombok:${LOMBOK_VERSION}"
    provided "org.roboguice:roboblender:${ROBOGUICE_VERSION}"

    compile project(':api')

    compile 'com.klinkerapps:link_builder:1.3.1@aar'
    compile 'com.github.paolorotolo:appintro:4.0.0'
    compile 'com.getbase:floatingactionbutton:1.10.1'

    compile "com.android.support:appcompat-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:support-v4:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:design:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:recyclerview-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:cardview-v7:${ANDROID_SUPPORT_VERSION}"
    compile "com.android.support:support-annotations:${ANDROID_SUPPORT_VERSION}"

    compile "com.google.android.gms:play-services-nearby:${PLAY_SERVICES_VERSION}"

    compile "org.roboguice:roboguice:${ROBOGUICE_VERSION}"

    testCompile project(':test_helper')
}